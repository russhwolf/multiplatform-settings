/*
 * Copyright 2019 Russell Wolf
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'kotlin-multiplatform'
}
apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply from: rootProject.file('gradle/publish.gradle')

kotlin {
    android {
        publishAllLibraryVariants()
    }
    jvm()
    iosArm64("ios")
    iosX64("iosSim")
    macosX64("macos")
    js {
        configure([compilations.main, compilations.test]) {
            tasks.getByName(compileKotlinTaskName).kotlinOptions {
                metaInfo = true
                sourceMap = true
                moduleKind = 'umd'
            }
        }
    }
    sourceSets {
        all {
            languageSettings {
                useExperimentalAnnotation('kotlin.Experimental')
            }
        }
        commonMain {
            dependencies {
                implementation kotlin("stdlib-common")
                
                implementation project(":multiplatform-settings")
            }
        }
        commonTest {
            dependencies {
                implementation project(":tests")
                
                implementation kotlin("test-common")
                implementation kotlin("test-annotations-common")
            }
        }

        androidMain {
            dependencies {
                implementation kotlin("stdlib")
            }
        }
        androidTest {
            dependencies {
                implementation kotlin("test")
                implementation kotlin("test-junit")
                implementation 'junit:junit:4.12'
                
                // TODO below dependencies can be removed after KT-29343 is fixed
                implementation project(":multiplatform-settings")
                implementation project(":tests")
            }
        }
        
        jvmMain {
            dependencies {
                implementation kotlin("stdlib")
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin("test")
                implementation kotlin("test-junit")
                implementation 'junit:junit:4.12'
            }
        }

        iosSimMain.dependsOn iosMain
        iosSimTest.dependsOn iosTest
        macosMain.dependsOn iosMain
        macosTest.dependsOn iosTest

        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 15
    }
}

task iosTest(dependsOn: 'linkTestDebugExecutableIosSim') {
    doLast {
        def binary = kotlin.targets.iosSim.compilations.test.getBinary('EXECUTABLE', 'DEBUG')
        exec {
            commandLine 'xcrun', 'simctl', 'spawn', "iPhone XÊ€", binary.absolutePath
        }
    }
}
tasks.check.dependsOn iosTest

// workaround for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}

apply from: rootProject.file('node-js.gradle')
apply from: rootProject.file('test-mocha-js.gradle')
